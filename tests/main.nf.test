nextflow_pipeline {

    name "Test Workflow EBIMETAGENOMICS_MIASSEMBLER"
    script "main.nf"

    test("Samplesheet spades") {

        tag "samplesheet"
        tag "SR"

        when {
            params {
                assembler   = "spades"
                samplesheet = "${projectDir}/tests/samplesheet/test.csv"
            }
        }

        then {
            with(workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 1
            }
        }

    }

    test("Samplesheet - no assembled - reads filtered") {

        tag "samplesheet"
        tag "SR"

        when {
            params {
                assembler                             = "spades"
                short_reads_low_reads_count_threshold = 1000000
                samplesheet                           = "${projectDir}/tests/samplesheet/test.csv"
            }
        }

        then {
            with(workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_BEFORE") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("FASTP") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("PHIX_READS_DECONTAMINATION") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_AFTER") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("CUSTOM_DUMPSOFTWAREVERSIONS") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("MULTIQC_STUDY") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 0
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 0
                // This process should not have been called
                assert trace.succeeded().count{ task -> task.name.contains("DOWNLOAD_FROM_FIRE") } == 0
            }
        }

    }

    test("metaSPAdes - paired end") {

        tag "ena-portal-api"
        tag "SR"

        when {

            params {
                // Force the assembly
                short_reads_filter_ratio_threshold   = 0.1
                short_reads_contig_threshold         = 1

                study_accession                      = "SRP115494"
                reads_accession                      = "SRR6180434"
            }
        }

        then {
            with (workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 0
                assert trace.succeeded().size() == 22
            }
        }

    }

    test("MEGAHIT - paired end") {

        tag "ena-portal-api"
        tag "SR"

        when {
            params {
                assembler                           = "megahit"

                // Force the assembly
                short_reads_filter_ratio_threshold  = 0.1

                study_accession                     = "SRP115494"
                reads_accession                     = "SRR6180434"
            }
        }

        then {
            with(workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 0
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 1
                assert trace.tasks().size() == 22
            }
        }

    }

    test("metaSPAdes - too few contigs") {

        tag "ena-portal-api"
        tag "SR"

        when {

            params {
                // Force the assembly
                short_reads_filter_ratio_threshold   = 0.1

                study_accession                      = "SRP115494"
                reads_accession                      = "SRR6180434"
            }
        }

        then {
            with (workflow) {
                // Cleaned assembly should contain 1 contig which fails the contig threshold and the pipeline stops
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 0
                // TODO fix this assert
                assert trace.succeeded().size() == 16
            }
        }

    }

    test("metaSPAdes - single end - should fail") {

        tag "ena-portal-api"
        tag "SR"

        when {
            params {
                assembler       = "metaspades"

                study_accession = "ERP012810"
                reads_accession = "ERR1076564"
            }
        }

        then {
            with(workflow) {
                // No contigs
                assert !success
                assert trace.failed().count{ task -> task.name.contains("SPADES") } == 1
            }
        }

    }

    test("MEGAHIT - single end - should produce an empty contigs file") {

        // TODO: fix along with - https://github.com/EBI-Metagenomics/miassembler/pull/21

        tag "ena-portal-api"
        tag "SR"

        when {
            params {
                study_accession = "ERP012810"
                reads_accession = "ERR1076564"
            }
        }

        then {
            with(workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 1
            }
        }
    }

    test("MEGAHIT - metaT - single end") {

        tag "ena-portal-api"
        tag "SR"

        when {
            params {
                study_accession = "DRP007622"
                reads_accession = "DRR280712"
            }
        }

        then {
            with(workflow) {
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 0
                assert trace.succeeded().count{ task -> task.name.contains("MEGAHIT") } == 1
                assert trace.tasks().size() == 22
            }
        }

    }

    test("Spades - metaT - single end should fail") {

        tag "ena-portal-api"
        tag "SR"

        when {
            params {
                assembler       = "spades"

                study_accession = "DRP007622"
                reads_accession = "DRR280712"
            }
        }

        then {
            with(workflow) {
                assert !success
                assert trace.failed().count{ task -> task.name.contains("SPADES") } == 1
            }
        }

    }

    test("Samplesheet spades - retries") {

        tag "samplesheet"
        tag "retries"
        tag "SR"

        when {
            params {
                assembler         = "spades"
                samplesheet       = "${projectDir}/tests/samplesheet/test_mem.csv"
                assembly_memory   = 0.5
                /* Memory jumping testing */
                /* will try with [0.5GB, 0.75GB, 1.13GB, ...]
                /* which rounds down to [0, 0, 1, ...]
                /* so should definitely fail twice before succeeding. after a few trys.
                /* ~~~ */
                max_spades_retries = 5
            }
        }

        then {
            with(workflow) {
                // eventual success:
                assert success
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 1

                // but failed and therefore retried multiple times first:
                assert trace.failed().count{ task -> task.name.contains("SPADES") } >= 2
            }
        }

    }

    test("Private study reads - this one should fail") {

        tag "samplesheet"
        tag "private"
        tag "SR"

        when {
            params {
                assembler     = "spades"
                samplesheet   = "${projectDir}/tests/samplesheet/test.csv"
                private_study = true
            }
        }

        // Complete this test when secrets are implemented in nf-test https://github.com/askimed/nf-test/issues/145
        then {
            with(workflow) {
                assert !success
                assert stdout.count{ line -> line.contains("Required secrets are missing") } == 1
            }
        }
    }

    test("Samplesheet long reads ONT and hifi HQ - hifi should fail") {

        tag "samplesheet"
        tag "LR"

        // we are forcing high-quality processing, but the data is in reality low-quality
        // therefore, frameshift correction is also triggered as low-quality is detected
        when {
            params {
                samplesheet     = "${projectDir}/tests/samplesheet/test_long_reads_HQ.csv"
                phix_reference  = null
            }
        }

        then {
            with(workflow) {
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_BEFORE") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_QC:FASTPLONG") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_QC:MINIMAP2_ALIGN_HUMAN") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_AFTER") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("PACBIO_HIFI:HIFIADAPTERFILT") } == 1

                assert trace.failed().count{ task -> task.name.contains("PACBIO_HIFI:FLYE") } == 1

                assert trace.succeeded().count{ task -> task.name.contains("PORECHOP_ABI") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("ONT_HQ:FLYE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_ASSEMBLY_QC:HUMAN_DECONTAMINATE_CONTIGS") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("FRAMESHIFT_CORRECTION:PROOVFRAME_MAP") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("FRAMESHIFT_CORRECTION:PROOVFRAME_FIX") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_ASSEMBLY_COVERAGE:LONG_READS_COVERAGE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("QUAST") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_ASSEMBLY_COVERAGE:CALCULATE_ASSEMBLY_COVERAGE") } == 1

                assert trace.tasks().size() == 24

                // This process should not have been called
                assert trace.succeeded().count{ task -> task.name.contains("SHORT_READS") } == 0
            }
        }
    }

    test("hifi - success") {

        tag "ena-portal-api"
        tag "LR"

        when {
            params {
                study_accession             = "SRP384046"
                reads_accession             = "SRR19888830"
                long_reads_assembler_config = "pacbio-hifi"
                phix_reference              = null
            }
        }

        then {
            with(workflow) {
                assert success

                assert trace.succeeded().count{ task -> task.name.contains("FETCHTOOL_READS") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_BEFORE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_QC:FASTPLONG") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_QC:MINIMAP2_ALIGN_HUMAN") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_AFTER") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("PACBIO_HIFI:HIFIADAPTERFILT") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("PACBIO_HIFI:FLYE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_ASSEMBLY_QC:HUMAN_DECONTAMINATE_CONTIGS") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("LONG_READS_COVERAGE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("CALCULATE_ASSEMBLY_COVERAGE") } == 1
                assert trace.succeeded().count{ task -> task.name.contains("QUAST") } == 1
            }
        }
    }

    test("Skip human decontamination - warning displayed and processes skipped") {

        tag "samplesheet"
        tag "skip_human_decontamination"
        tag "SR"

        when {
            params {
                assembler                   = "spades"
                samplesheet                 = "${projectDir}/tests/samplesheet/test.csv"
                skip_human_decontamination  = true

                human_reference             = null
                phix_reference              = "phix"
            }
        }

        then {
            with(workflow) {
                assert success

                // Verify human decontamination processes are not executed
                assert trace.succeeded().count{ task -> task.name.contains("MINIMAP2_ALIGN_HUMAN") } == 0
                assert trace.succeeded().count{ task -> task.name.contains("HUMAN_DECONTAMINATE_CONTIGS") } == 0
                assert trace.succeeded().count{ task -> task.name.contains("SPADES") } == 2
                assert trace.succeeded().count{ task -> task.name.contains("PHIX_READS_DECONTAMINATION") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("FASTQC_BEFORE") } == 3
                assert trace.succeeded().count{ task -> task.name.contains("MULTIQC_STUDY") } == 2
            }
        }
    }

    test("Skip human decontamination - throw an error if the human_reference is provided and skip human decont is true") {

        tag "samplesheet"
        tag "skip_human_decontamination"
        tag "SR"

        when {
            params {
                assembler                   = "spades"
                samplesheet                 = "${projectDir}/tests/samplesheet/test.csv"
                // It's incompatible to try to skip the decontamination of human data while setting the human_reference. We strongly discourage skipping this step.
                skip_human_decontamination  = true
                human_reference             = "human"
            }
        }

        then {
            with(workflow) {
                assert !success
            }
        }
    }
}
